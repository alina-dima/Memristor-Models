* SPICE model for 10 um Nb:SrTiO3 memristor
* Based on the Yakopcic (2020) model
* Last Update: 19/06/2022
*
* Connections:
* TE - top electrode
* BE - bottom electrode
* XSV - External connection to plot state variable
* that is not used otherwise

.subckt SRTIO3 TE BE XSV

* Fitting parameters to model different devices
* gmax_p, bmax_p, gmax_n, bmax_n:      Parameters for OFF state IV relationship
* gmin_p, bmin_p, gmin_n, bmin_n:      Parameters for OFF state IV relationship
* Vp, Vn:                              Pos. and neg. voltage thresholds
* Ap, An:                              Multiplier for SV motion intensity
* xp, xn:                              Points where SV motion is reduced
* alphap, alphan:                      Rate at which SV motion decays
* xo:                                  Initial value of SV
* eta:                                 SV direction relative to voltage

.param gmax_p=0.0006158328101 bmax_p=5.187989524 gmax_n=3.26e-5 bmax_n=6.062818548
+      gmin_p=0.08546184476 bmin_p=0.06881418488 gmin_n=0.001672677617 bmin_n=2.124988927
+      Ap=0.071 An=0.02428911723
+      Vp=0 Vn=0
+      xp=0.11 xn=0.09869126936
+      alphap=9.2 alphan=0.2497672659
+      xo=0 eta=1

* Multiplicative functions to ensure zero state
* variable motion at memristor boundaries
.func wp(V) = xp/(1-xp) - V/(1-xp) + 1
.func wn(V) = V/xn

* Function G(V(t)) - Describes the device threshold
.func G(V) = 
+    IF(V < -Vn, 
+       -An*(exp(-V) - exp(Vn)), 
+       IF(V > Vp, 
+           Ap*(exp(V) - exp(Vp)), 
+           0 ) )

* Function F(V(t),x(t)) - Describes the SV motion 
.func F(V1,V2) = 
+    IF(eta*V1 > 0, 
+        IF(V2 >= xp, 
+            exp(-alphap*(V2 - xp))*wp(V2), 
+            1 ), 
+        IF(V2 <= xn, 
+            exp(alphan*(V2 - xn ))*wn(V2), 
+            1 ) )

* IV Response - Hyperbolic sine due to MIM structure
.func IVRel(V1,V2) = 
+    IF(V1 >= 0, 
+       gmax_p*sinh(bmax_p*V1)*V2 + gmin_p*(1-exp(-bmin_p*V1))*(1-V2), 
+       gmax_n*(1-exp(-bmax_n*V1))*V2 + gmin_n*sinh(bmin_n*V1)*(1-V2) 
+       )

* Circuit to determine state variable
* dx/dt = eta*F(V(t),x(t))*G(V(t))
Cx XSV 0 {1}
.ic V(XSV) = xo
Gx 0 XSV value = {eta*F(V(TE,BE),V(XSV,0))*G(V(TE,BE))}
* Current source for memristor IV response
Gm TE BE value = {IVRel(V(TE,BE),V(XSV,0))}

.ends SRTIO3